buildscript {
  ext {
    springBootVersion = '1.3.0.RELEASE'
  }
  repositories {
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.+'
  }
}

group = "ru.ratauth"

allprojects {
  apply plugin: 'idea'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'provided-base'

  repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
    maven { url "https://repo.spring.io/milestone" }
  }
  configurations {
    compile.exclude group: 'org.springframework.boot', module:'spring-boot-starter-tomcat'
  }

  dependencyManagement {
    imports {
      mavenBom 'io.spring.platform:platform-bom:2.0.0.RELEASE'
    }

    dependencies {
      dependency 'com.google.guava:guava:18.0'
      dependency 'org.codehaus.groovy:groovy-all:2.4.5'
      dependency 'org.spockframework:spock-core:1.0-groovy-2.4'
      dependency 'org.spockframework:spock-spring:1.0-groovy-2.4'
      dependency 'org.projectlombok:lombok:1.16.2'
      dependency 'com.nimbusds:nimbus-jose-jwt:4.3.1'
      dependency 'io.reactivex:rxjava:1.0.15'
      dependency 'io.ratpack:ratpack-rx:1.1.1'
      dependency 'io.ratpack:ratpack-spring-boot:1.1.1'
      dependency 'io.ratpack:ratpack-groovy:1.2.0'
      dependency 'io.ratpack:ratpack-test:1.1.1'
      dependency 'org.apache.commons:commons-lang3:3.4'
    }
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'groovy'
  version = '0.2.0'
  group = 'ru.ratauth'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  jar {
    archiveName = project.name + ".jar"
  }

  dependencies {
    compile 'org.codehaus.groovy:groovy-all'
    compile 'org.projectlombok:lombok'
    compile 'io.reactivex:rxjava'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.spockframework:spock-core', {
      exclude module: "groovy-all"
    }
    testCompile 'org.spockframework:spock-spring', {
      exclude module: "groovy-all"
    }
    testCompile 'io.ratpack:ratpack-test'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
}

idea {
  project {
    jdkName "1.8"
    languageLevel "1.8"
    ipr {
      withXml { provider ->
        def node = provider.asNode()
        //configure git support for the project in idea
        node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
      }
    }
  }
}